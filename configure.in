dnl Process this file with autoconf to produce a configure script.
AC_DEFUN([m80version],[0.07])
AC_DEFUN([m80veryMinor],[33])
AC_INIT(m80,[m80version].[m80veryMinor])
AC_CONFIG_SRCDIR(src)
AM_INIT_AUTOMAKE

AC_PREFIX_DEFAULT([/usr/local/m80-m80version])

# code to check for GNU m4.
AC_PATH_PROG([M4],[m4])
AC_PATH_PROG([ECHO],[echo])
AC_PATH_PROG([PERL],[perl])
AC_PATH_PROG([BASH],[bash])
AC_PATH_PROG([MAKE],[make])
AC_PATH_PROG([MAKEINFO],[makeinfo])
AC_PATH_PROG([WGET],[wget])


AC_DEFUN([MY_ARG_WITH],
[AC_ARG_WITH([$1],
             AC_HELP_STRING([--with-$1], [use $1 (default is $2)]),
             ac_cv_use_$1=$withval, ac_cv_use_$1=no)
AC_CACHE_CHECK(whether to use $1, ac_cv_use_$1, ac_cv_use_$1=$2)])

AC_DEFUN([MY_ARG_ENABLE],
[AC_ARG_ENABLE([$1],
             AC_HELP_STRING([--enable-$1], [use $1 (default is $2)]),
             ac_cv_use_$1=$enableval, ac_cv_use_$1=no)
AC_CACHE_CHECK(whether to use $1, ac_cv_use_$1, ac_cv_use_$1=$2)])

AC_DEFUN([SET_VC],
[AC_ARG_WITH([$1],
             AC_HELP_STRING([--with-$1], [use $1 for the version control tool $2]),
	     [
	     VC_TYPE=$1
	     ],[])
])

SET_VC(perforce)
SET_VC(cvs)
SET_VC(pvcs,[(not implemented)])
SET_VC(codecenter,[(not implemented)])
SET_VC(rcs,[(not implemented)])
SET_VC(vss,[(not implemented ... and you need to get a life)])

AC_CACHE_CHECK([your version control type],
               [VC_TYPE],[])

AC_SUBST(VC_TYPE)


AC_MSG_CHECKING([for GNU m4])
${M4} --help >/dev/null 2>&1
if test $? -ne 0; then
  AC_MSG_RESULT([no])
  AC_MSG_ERROR([make sure GNU m4 is picked first in your path])
else
  AC_MSG_RESULT([yes])
fi

AC_MSG_CHECKING([for GNU make])
${MAKE} --help >/dev/null 2>&1
if test $? -ne 0; then
  AC_MSG_RESULT([no])
  AC_MSG_ERROR([make sure GNU make is picked first in your path])
else
  AC_MSG_RESULT([yes])
fi

AC_MSG_CHECKING([for working echo -n])
for x in /bin/echo /usr/bin/echo /usr/ucb/echo echo; do
    lines=`$x -n | wc -l` > /dev/null 2>&1
    if test $lines -eq 0 -a $? -eq 0 ; then
       PRINTDASHN="$x"
       AC_MSG_RESULT([${PRINTDASHN}])
       break
    fi
done

MAKEINFO_REQUIRED_VERSION=4.2
AC_MSG_CHECKING([for texinfo version >= ${MAKEINFO_REQUIRED_VERSION}])
makeinfoVersion=`${MAKEINFO} --version | awk 'NR == 1 && $NF >= '${MAKEINFO_REQUIRED_VERSION}' {print $NF}'`
if test -z "${makeinfoVersion=}"; then
  AC_MSG_RESULT([no])
  AC_MSG_ERROR([You need texinfo version >= ${MAKEINFO_REQUIRED_VERSION} .... its not that hard really; see http://www.gnu.org/software/texinfo/])
else
  AC_MSG_RESULT([yes])
fi


if test -z "$PRINTDASHN" ; then
   AC_MSG_ERROR([could find not an echo that understands the -n flag])
fi

AC_SUBST(PRINTDASHN)

AC_DEFUN([AC_CHECK_PERL_MODULES],[dnl
AC_MSG_CHECKING([for perl module $1])
$PERL -M$1 -e exit >/dev/null 2>&1
if test $? -ne 0; then
  AC_MSG_RESULT([no])
  AC_MSG_ERROR([you should fire up "$PERL -MCPAN -e 'install $1'", and then re-run configure])
else
  AC_MSG_RESULT([yes])
fi
])

# code to conditionally check compilation libs
# currently this only checks if the user is
# setup to run in a particular configuration - it doesn't
# condidtionally compile different systems based on these settings.

AC_CHECK_PERL_MODULES(Data::Dumper)
AC_CHECK_PERL_MODULES(FileHandle)
AC_CHECK_PERL_MODULES(Getopt::Long)
AC_CHECK_PERL_MODULES(IO::File)
AC_CHECK_PERL_MODULES(Pod::Usage)
AC_CHECK_PERL_MODULES(Carp)
AC_CHECK_PERL_MODULES(File::Copy)
AC_CHECK_PERL_MODULES(File::Basename)
AC_CHECK_PERL_MODULES(POSIX)

MY_ARG_ENABLE([xml], [no])
test "x$ac_cv_use_xml" = "xyes" && {
AC_CHECK_PERL_MODULES(XML::XPath)
AC_CHECK_PERL_MODULES(XML::Simple)
}

MY_ARG_ENABLE([maintainer], [no])
test "x$ac_cv_use_maintainer" = "xyes" && {
AC_PATH_PROG([CURL],[curl])
AC_CHECK_PERL_MODULES(WWW::Mechanize)
AC_CHECK_PERL_MODULES(LWP::Request)
AC_CHECK_PERL_MODULES(Crypt::SSLeay)
AC_CHECK_PERL_MODULES(Pod::Simple::Wiki)
}

# this is convienent for genning this statement:
# f Makefile.am | grep -v '~' | cut -c3- | sed -e 's/.am//g' | tr "\n" " " | perl -nle 'print "AC_CONFIG_FILES([$_])";'

AC_CONFIG_FILES([doc/Makefile lib/db/generic/Makefile lib/db/Makefile lib/db/oracle/Makefile lib/db/postgresql/Makefile lib/m4/Makefile lib/make/Makefile lib/Makefile lib/perl/m80/db/Makefile lib/perl/m80/generator/Makefile lib/perl/m80/Makefile lib/perl/Net/Makefile lib/perl/m80/util/Makefile lib/perl/Makefile lib/shell/db/Makefile lib/shell/db/oracle/Makefile lib/shell/env/Makefile lib/shell/Makefile lib/texi/Makefile lib/xml/Makefile Makefile src/Makefile src/util/Makefile templates/autoconfTemplate/Makefile templates/autoconfTemplate/autofiles/Makefile templates/generic/Makefile templates/m80xmlRepos/bdfs/Makefile templates/m80xmlRepos/envs/Makefile templates/m80xmlRepos/Makefile templates/m80xmlRepos/projects/Makefile templates/Makefile tests/Makefile tests/perl/Makefile tests/shell/Makefile lib/perl/m80/Helpers/Makefile])

# src/fxcmd/Makefile 

AC_OUTPUT



